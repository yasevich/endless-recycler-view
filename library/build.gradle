apply plugin: 'com.android.library'
apply plugin: 'bintray-release'

ext {
    versionMajor = 1
    versionMinor = 0
    versionPatch = 0
}

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode libVersionCode()
        versionName libVersionName()
    }
}

dependencies {
    compile 'com.android.support:recyclerview-v7:23.1.1'
}

publish {
    userOrg = 'slava'
    groupId = 'com.github.yasevich'
    artifactId = 'endless-recycler-view'
    version = libVersionName()
    website = 'https://github.com/yasevich/endless-recycler-view'
    issueTracker = "${website}/issues"
    repository = "${website}.git"
}

def libVersionCode() {
    return versionMajor * 10000 + versionMinor * 100 + versionPatch
}

def libVersionName() {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}